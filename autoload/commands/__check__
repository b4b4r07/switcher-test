#!/bin/zsh

__import "core/core"
__import "print/print"
__import "job/spinner"

local    is_verbose=false
local    line
local -A zspec
local -a fail args

while (( $# > 0 ))
do
    case "$1" in
        --verbose)
            is_verbose=true
            ;;
        -*|--*)
            __die "$1: Unknown option\n"
            return 1
            ;;
        *)
            args+=("$1")
            ;;
    esac
    shift
done

exists() {
    local from dir

    from="$1"
    dir="$2"
    case "$from" in
        "local")
            return 0
            ;;
        "oh-my-zsh")
            if [[ -d ${dir:h} ]]; then
                return 0
            fi
            ;;
        *)
            if [[ -d $dir ]]; then
                return 0
            fi
            ;;
    esac

    return 1
}

for line in ${(u)${args[@]:gs:@::}}
do
    zspec=( ${(@f)"$(__parser__ "$line")"} )
    if [[ $zspec[from] == "local" ]]; then
        continue
    fi
    if ! exists "$zspec[from]" "$zspec[dir]"; then
        fail+=("$zspec[name]")
    fi
    # if [[ $zspec[from] != "oh-my-zsh" && ! -e $zspec[dir] ]] || [[ $zspec[from] == "oh-my-zsh" && ! -d ${zspec[dir]:h} ]]; then
    #     fail+=("$zspec[name]")
    # fi
done

if (( $#fail > 0 )); then
    if $is_verbose; then
        __put "- $fg[red]%s$reset_color: not installed\n" "${(@nO)fail}"
    fi
    return 1
else
    return 0
fi
