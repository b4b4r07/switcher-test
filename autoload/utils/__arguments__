#!/bin/zsh

__import "core/core"

local    arg
local -a cmds ud_cmds

arg="$1"
if [[ -z $arg ]]; then
    __die "too few arguments\n"
    return 1
fi

ud_cmds=( ${^path}/zplug-*(N-.:t:gs:zplug-:) )
if  [[ -n ${(M)ud_cmds:#$arg} ]]; then
    eval "$commands[zplug-$arg]"
    return $status
fi

__get_autoload_files \
    "$ZPLUG_ROOT/autoload/commands"
reply+=("${ud_cmds[@]}")
cmds=(
${(@f)"$(awk \
    -f "$ZPLUG_ROOT/src/share/fuzzy.awk" \
    -v search_string="$arg" \
    <<<"${(F)reply:gs:_:}"
)"}
)

case $#cmds in
    0)
        __die "$arg: no such command\n"
        return 1
        ;;
    1)
        __die "WARNING: You called a zplug command named '%s', which does not exist.\n" "$arg"
        __die "Continuing under the assumption that you meant '%s'\n" "$cmds"
        zplug "$cmds"
        return $status
        ;;
    *)
        __die "zplug: '%s' is not a zplug command. See 'zplug help'.\n" "$arg"
        __die "Did you mean one of these?\n"
        __die "        %s\n" ${cmds}
        return 1
        ;;
esac
